# Maintainer: Pekka Helenius <fincer89 [at] hotmail [dot] com>
# Contributor: Adrià Cereto i Massagué <ssorgatem at gmail.com>

pkgname=d9vk-git
pkgver=m0.12.r0.g6b125834
pkgrel=1
pkgdesc="A Vulkan-based compatibility layer for Direct3D 9 which allows running 3D applications on Linux using Wine"
url="https://github.com/Joshua-Ashton/d9vk"
license=('custom:zlib/libpng')

arch=('x86_64')
conflicts=(
  'd9vk-bin'
  'd9vk-mingw'
  'd9vk-winelib'
  'd9vk-mingw-git'
  'd9vk-winelib-git'
)
depends=('wine' 'vulkan-icd-loader' 'lib32-vulkan-icd-loader')
makedepends=(
  'ninja'
  'meson>=0.43'
  'glslang'
  'git'
  'wine'
)
provides=('d9vk')
source=(${pkgname}::"git+https://github.com/Joshua-Ashton/d9vk.git")
sha256sums=('SKIP'
            'SKIP'
            'b67f3a88115b1d048fb373ff6df2140c3027fe9edecccf58c1a88efce01fb738')

##############################

# Copy custom patches to correct location
p=0
for patch in $(find . -mindepth 1 -maxdepth 1 -regex ".*\.\(patch\|diff\)$"); do
  patches[$p]="$(basename ${patch})"
  let p++
done
unset p
source+=(${source[@]} ${patches[@]})

# Reset git tree and apply custom patches
prepare() {

  cd "${srcdir}/${pkgname}"

  # Apply patches, if present
  if [[ $(ls "${srcdir}"/*.{patch,diff} 2>/dev/null | wc -w) -ne 0 ]]; then

  # Apply all custom patches
  msg2 "Applying custom patches..."

    for i in "${srcdir}"/*.{patch,diff}; do 
      if [[ -f ${i} ]]; then
        patch -Np1 < ${i}
      fi
    done

  fi

}

##############################

pkgver() {
  cd "$srcdir/${pkgname}"
  git describe --long --tags --always | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

build() {
  cd "$srcdir/${pkgname}"
  ./package-release.sh ${pkgver} build --no-package
}

package() {
  mkdir -p  "$pkgdir"/usr/share/dxvk/{x32,x64}

  cd "$srcdir/${pkgname}"
  install -m0644 build/dxvk-${pkgver}/x32/d3d9.dll "$pkgdir"/usr/share/dxvk/x32/d3d9.dll
  install -m0644 build/dxvk-${pkgver}/x64/d3d9.dll "$pkgdir"/usr/share/dxvk/x64/d3d9.dll
  install -m0755 build/dxvk-${pkgver}/setup_dxvk.sh "$pkgdir"/usr/share/dxvk/setup_d9vk.sh

  mkdir "$pkgdir"/usr/bin
  cd "$pkgdir"/usr/bin
  ln -s /usr/share/dxvk/setup_d9vk.sh setup_d9vk
}
